plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "com.github.passit.project-application"
}

def mockPropertiesFile = rootProject.file("template.properties")

def appPropertiesFile = rootProject.file("app.properties")
def appProperties = new Properties()

if (appPropertiesFile.exists()) {
    appProperties.load(new FileInputStream(appPropertiesFile))
} else {
    appProperties.load(new FileInputStream(mockPropertiesFile))
}

android {
    signingConfigs {
        if (appPropertiesFile.exists()) {
            release {
                storeFile rootProject.file(appProperties["storeFile"])
                keyAlias appProperties["keyAlias"]
                keyPassword appProperties["keyPassword"]
                storePassword appProperties["storePassword"]
            }
        }
    }
    compileSdkVersion Android.compileSdkVersion
    buildToolsVersion Android.buildToolsVersion

    defaultConfig {
        applicationId Android.applicationId
        minSdkVersion Android.minSdkVersion
        targetSdkVersion Android.targetSdkVersion
        versionCode Android.versionCode
        versionName Android.versionName

        multiDexEnabled = true

        testInstrumentationRunner = getInstrumentation()
    }

    buildTypes {
        debug {
            buildConfigField("String", "TEST_TAGS", "\"" + getTestTags() + "\"")
            buildConfigField "String", "TEST_SCENARIO", "\"" + getTestScenario() + "\""
            debuggable true
        }
        release {
            minifyEnabled true
            debuggable false
            shrinkResources true
            zipAlignEnabled true
            renderscriptDebuggable false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            if (appPropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi", "-Xopt-in=kotlin.RequiresOptIn"]
    }
}

def getTestTags() {
    project.getProperties().get("tags") ?: ""
}

def getTestScenario() {
    project.getProperties().get("scenario") ?: ""
}

def getInstrumentation() {
    project.hasProperty("bdd") ? "com.github.passit.runner.CucumberInstrumentationRunner" : "androidx.test.runner.AndroidJUnitRunner"
}


dependencies {
    implementation Libs.kotlinStd
    implementation Libs.appcompat
    implementation Libs.activity

    // aws amplify
    Libs.amplify.each { amplify -> implementation amplify }

    // hilt dependency injection
    implementation Libs.hiltAndroid
    implementation Libs.hiltLifecycleViewModel
    kapt Libs.hiltCompiler
    kapt Libs.hiltAndroidCompiler

    // material design
    implementation(Libs.materialDesign)

    // navigation fragment
    implementation Libs.fragment
    implementation Libs.navigationFragment
    implementation Libs.navigationUi

    // coroutines
    implementation Libs.coroutines

    implementation Libs.viewpager

    implementation Libs.gson

    implementation Libs.multidex
    implementation Libs.legacy

    implementation Libs.coreKtx

    implementation Libs.constraintLayout
    implementation Libs.coordinatorLayout

    // square picasso
    implementation Libs.squarePicasso

    // dexter
    implementation Libs.dexter

    // uCrop
    implementation Libs.ucrop

    // room
    implementation Libs.roomRuntime
    kapt Libs.roomCompiler
    implementation Libs.roomKotlinExt

    // paging
    implementation Libs.pagingRuntime

    testImplementation TestLibs.junit
    testImplementation TestLibs.extJunit
    testImplementation TestLibs.roomTesting

    androidTestImplementation TestLibs.extJunit
    androidTestImplementation TestLibs.espressoCore

    // bdd
    androidTestImplementation TestLibs.cucumber
    androidTestImplementation TestLibs.rules
    androidTestImplementation TestLibs.runner
    androidTestImplementation TestLibs.espressoIntents
}

amplify {
    backendStackName "AppStack"
    amplifyTemplate file("amplifyconfiguration.mustache")
}
